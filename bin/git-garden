#!/usr/bin/perl

=head1 SYNOPSIS

    perl bin/git-garden --help

    perl bin/git-garden --git_dir t/repos/test -t gnuplot
    firefox simple.png

    perl bin/git-garden --git_dir t/repos/test
    firefox simple.html

=cut

use strict;
use warnings;
use 5.010;

use lib ( 'lib', '../lib' );
use Getopt::Long::Descriptive;

use Git::Garden 'create_git_graph_grid';
use Git::Garden::Importer::gitpp;
use Git::Garden::Importer::gitclass;
use Git::Garden::Render::html;
use Git::Garden::Render::gnuplot;
use Git::Garden::Render::graphviz;

use File::Slurp 'write_file';

$|++;

main();

sub main {
    my ( $opt, $usage ) = describe_options(
        'git-garden %o',    #
        [],
        [ 'git_dir|d=s',     "path to the git repository you want to graph, can be either to the checkout, the .git dir, or even any renamed git metadata dir. Default: .", { default => '.' } ],
        [ 'import_type|i=s', "Method to use to import git data. ( gitpp, gitclass ) Default: gitpp",                                                                        { default => 'gitpp' } ],
        [ 'dump_type|t=s',   "Format in which to dump the generated data. ( html, gnuplot, graphviz ) Default: html",                                                       { default => 'html' } ],
        [],
        [ 'help|h', "print usage message and exit" ],
    );
    print( $usage->text ), exit if $opt->help;

    my $importer_class = "Git::Garden::Importer::" . $opt->import_type;
    my $commits = $importer_class->prepare_commits( $opt->git_dir );

    my $grid = create_git_graph_grid( $commits );

    my $render_class = "Git::Garden::Render::" . $opt->dump_type;
    my $out = $render_class->plot_grid( $grid );

    return if !$out;

    my $extension = "";
    $extension = ".html" if $opt->dump_type eq 'html';
    $extension = ".png" if $opt->dump_type eq 'graphviz';

    write_file "simple$extension", { binmode => ':raw' }, $out;

    return;
}
